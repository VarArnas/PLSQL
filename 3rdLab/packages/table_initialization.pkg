REATE OR REPLACE PACKAGE table_initialization_pkg
AUTHID CURRENT_USER
IS
    TYPE insert_values_nt IS TABLE OF VARCHAR2(4000);
    TYPE sign_name_nt IS TABLE OF VARCHAR2(128);
    TYPE pairs_nt IS TABLE OF sekmingaPora%ROWTYPE;
    TYPE signs_nt IS TABLE OF zenklas%ROWTYPE;
    TYPE years_nt IS TABLE OF sekmingiVyruMetai%ROWTYPE;
    TYPE marriage_nt IS TABLE OF santuoka%ROWTYPE;

    c_default_sign_values CONSTANT sign_name_nt := sign_name_nt('Aries', 'Taurus', 'Gemini', 'Cancer', 'Leo', 'Virgo', 'Libra', 'Scorpio', 'Sagittarius', 'Capricorn', 'Aquarius', 'Pisces');
    c_min_year CONSTANT NUMBER(4) := 2020;
    c_max_year CONSTANT NUMBER(4) := 2025;
    c_start_date CONSTANT DATE := TO_DATE('01-JAN-2020', 'DD-MON-YYYY');
    
    PROCEDURE dynamic_insert(i_table_name VARCHAR2, i_column_values insert_values_nt);
    FUNCTION create_random_sekmingos_poros(i_amount PLS_INTEGER) RETURN pairs_nt;
    FUNCTION create_random_sekmingi_metai(i_amount PLS_INTEGER, i_table_name VARCHAR2) RETURN years_nt;
    FUNCTION create_random_santuoka(i_amount PLS_INTEGER) RETURN marriage_nt;
END table_initialization_pkg;
/

CREATE OR REPLACE PACKAGE BODY table_initialization_pkg
IS
    l_signs sign_name_nt;

    PROCEDURE fetch_signs 
    IS
        CURSOR cur_signs IS SELECT zenklas FROM zenklas;
    BEGIN
        IF l_signs IS NULL THEN
            OPEN cur_signs;
            FETCH cur_signs BULK COLLECT INTO l_signs;
            CLOSE cur_signs;
        END IF;
    EXCEPTION
        WHEN OTHERS THEN
            exception_pkg.handle_exception;
            IF cur_signs%ISOPEN THEN CLOSE cur_signs; END IF;
            RAISE;
    END fetch_signs;

    FUNCTION create_random_santuoka(i_amount PLS_INTEGER) 
    RETURN marriage_nt
    IS
        l_marriages marriage_nt := marriage_nt();
    BEGIN
        fetch_signs;

        <<generate_random_years>>
        DECLARE
            l_currval NUMBER;
        BEGIN
            FOR indx IN 1..i_amount
            LOOP
                l_marriages.EXTEND;
                SELECT santuoka_seq.NEXTVAL INTO l_currval FROM dual;
                l_marriages(indx).numeris := l_currval;
                l_marriages(indx).registracijosData := c_start_date + TRUNC(DBMS_RANDOM.VALUE(0, SYSDATE - c_start_date + 1));
                l_marriages(indx).motersZenklas := l_signs(TRUNC(DBMS_RANDOM.VALUE(1, l_signs.COUNT + 1)));
                l_marriages(indx).vyroZenklas := l_signs(TRUNC(DBMS_RANDOM.VALUE(1, l_signs.COUNT + 1)));
            END LOOP;
        END generate_random_years;

        RETURN l_marriages;
    END create_random_santuoka;

    FUNCTION create_random_sekmingi_metai(i_amount PLS_INTEGER, i_table_name VARCHAR2) 
    RETURN years_nt
    IS
        l_years years_nt := years_nt();
    BEGIN
        fetch_signs;

        <<generate_random_years>>
        DECLARE
            l_currval NUMBER;
            sql_stmt table_creation_pkg.sql_string_t := 'SELECT ' || i_table_name || '_seq.NEXTVAL FROM dual';
        BEGIN
            FOR indx IN 1..i_amount
            LOOP
                l_years.EXTEND;
                EXECUTE IMMEDIATE sql_stmt INTO l_currval;
                l_years(indx).numeris := l_currval;
                l_years(indx).metai := TRUNC(DBMS_RANDOM.VALUE(c_min_year, c_max_year + 1));
                l_years(indx).zenklas := l_signs(TRUNC(DBMS_RANDOM.VALUE(1, l_signs.COUNT + 1)));
            END LOOP;
        EXCEPTION
            WHEN OTHERS THEN
                exception_pkg.handle_exception(sql_stmt);
                RAISE; 
        END generate_random_years;

        RETURN l_years;
    END create_random_sekmingi_metai;

    FUNCTION create_random_sekmingos_poros(i_amount PLS_INTEGER)
    RETURN pairs_nt
    IS
        l_pairs pairs_nt := pairs_nt();
    BEGIN
        fetch_signs;

        <<generate_random_poros>>
        DECLARE
            l_currval NUMBER;
        BEGIN
            FOR indx IN 1..i_amount 
            LOOP
                l_pairs.EXTEND;
                SELECT sekmingaPora_seq.NEXTVAL INTO l_currval from dual;
                l_pairs(indx).numeris := l_currval;
                l_pairs(indx).motersZenklas :=  l_signs(TRUNC(DBMS_RANDOM.VALUE(1, l_signs.COUNT + 1)));
                l_pairs(indx).vyroZenklas := l_signs(TRUNC(DBMS_RANDOM.VALUE(1, l_signs.COUNT + 1)));
            END LOOP;
        END generate_random_poros;

        RETURN l_pairs;
    END;

    PROCEDURE dynamic_insert(
        i_table_name VARCHAR2,
        i_column_values insert_values_nt
    ) IS
        v_cursor      INTEGER;
        v_columns     insert_values_nt;
        v_result      INTEGER;
        v_sql         table_creation_pkg.sql_string_t;
        v_col_count   NUMBER;
        v_batch_count NUMBER;
        CURSOR cur_column_names(p_table_name IN VARCHAR2) IS SELECT column_name
                                                            FROM user_tab_columns
                                                            WHERE table_name = UPPER(p_table_name)
                                                            ORDER BY column_id; 
    BEGIN
        <<get_column_names>>
        BEGIN
            OPEN cur_column_names(i_table_name);
            FETCH cur_column_names BULK COLLECT INTO v_columns;
            CLOSE cur_column_names;
        EXCEPTION
            WHEN OTHERS THEN
                exception_pkg.handle_exception;
                IF cur_column_names%ISOPEN THEN
                    CLOSE cur_column_names;
                END IF;
                RAISE;
        END get_column_names;
        
        v_col_count := v_columns.COUNT;
        v_batch_count := i_column_values.COUNT / v_columns.COUNT;
        
        v_sql := 'INSERT ALL ';
        <<construct_row_insertion>>
        DECLARE
            l_start PLS_INTEGER := 1;
            l_end PLS_INTEGER := v_col_count;
        BEGIN
            <<iterate_each_row>>
            FOR indx IN 1..v_batch_count
            LOOP
                v_sql := v_sql || 'INTO ' || i_table_name || ' VALUES (';

                <<iterate_each_column>>
                FOR i IN l_start..l_end LOOP
                    v_sql := v_sql || ':' || i;
                    IF i < l_end THEN v_sql := v_sql || ','; END IF;
                END LOOP iterate_each_column;

                l_start := l_end + 1;
                l_end := l_end + v_col_count;
                v_sql := v_sql || ') ';
            END LOOP iterate_each_row;
        END construct_row_insertion;
        v_sql := v_sql || ' SELECT * FROM dual';
        
        v_cursor := DBMS_SQL.OPEN_CURSOR;
        DBMS_SQL.PARSE(v_cursor, v_sql, DBMS_SQL.NATIVE);
        
        FOR i IN 1..i_column_values.COUNT LOOP
            DBMS_SQL.BIND_VARIABLE(v_cursor, ':' || i, i_column_values(i));
        END LOOP;
        
        v_result := DBMS_SQL.EXECUTE(v_cursor);
        DBMS_SQL.CLOSE_CURSOR(v_cursor);    
    EXCEPTION
        WHEN OTHERS THEN
            IF DBMS_SQL.IS_OPEN(v_cursor) THEN
                DBMS_SQL.CLOSE_CURSOR(v_cursor);
            END IF;
            RAISE;
    END dynamic_insert;
END table_initialization_pkg;
/
